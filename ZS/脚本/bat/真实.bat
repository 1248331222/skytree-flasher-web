@echo off
setlocal enabledelayedexpansion
chcp 65001 >nul
title "移植线刷工具"
color 3f

set "CURRENT_DIR=%~dp0"
set "TOOL_PATH=%CURRENT_DIR%tools\fastboot.exe"
set "IMG_DIR=%CURRENT_DIR%images"

if not exist "%TOOL_PATH%" (
    cls
    echo.
    echo "[ERROR] 未找到 fastboot.exe"
    pause
    exit
)

cls
echo ==========================================================
echo "          一加Turbo6 移植 OnePlus 15 "
echo "              作者：酷安 空白没有输"
echo ==========================================================
echo "警告：刷机前请备份数据，风险自负！"
echo "默认去除引导配置切记不要低于Coloros16"
echo "警告：在继续之前，请确保已备份所有重要数据。风险自负。"
echo.
echo "操作步骤："
echo "1. 请在手机设置中，启用“USB调试”模式。"
echo "2. 如果脚本卡在 < waiting for any device >，请安装Fastboot驱动。"
echo "3. 手动将手机重启至 Fastboot 模式（关机状态下同时按住 音量下键 和 电源键）。"
echo "4. 使用USB数据线将手机连接到电脑。"
echo.
echo "请确认以上步骤已完成，然后按任意键开始刷机..."
echo ==========================================================
pause >nul

echo.
echo "[检测设备] 等待设备连接..."
"%TOOL_PATH%" devices
if errorlevel 1 (
    echo "[错误] 未检测到设备，请检查驱动与连接"
    pause >nul
    exit /b
)

set "SKIP_LIST=modem vbmeta vbmeta_system vbmeta_vendor super"

echo.
echo "[1/6] 刷入基带 modem A+B 槽位"
if exist "%IMG_DIR%\modem.img" (
    "%TOOL_PATH%" flash modem_a "%IMG_DIR%\modem.img"
    "%TOOL_PATH%" flash modem_b "%IMG_DIR%\modem.img"
)

echo.
echo " 刷入 recovery 分区..."
if exist "%IMG_DIR%\recovery.img" (
    "%TOOL_PATH%" flash recovery "%IMG_DIR%\recovery.img"
)

echo.
echo "[2/6] 禁用AVB 刷入 vbmeta 系列 A+B 槽位"
if exist "%IMG_DIR%\vbmeta.img" (
    "%TOOL_PATH%" --disable-verity --disable-verification flash vbmeta_a "%IMG_DIR%\vbmeta.img"
    "%TOOL_PATH%" --disable-verity --disable-verification flash vbmeta_b "%IMG_DIR%\vbmeta.img"
)
if exist "%IMG_DIR%\vbmeta_system.img" (
    "%TOOL_PATH%" --disable-verity --disable-verification flash vbmeta_system_a "%IMG_DIR%\vbmeta_system.img"
    "%TOOL_PATH%" --disable-verity --disable-verification flash vbmeta_system_b "%IMG_DIR%\vbmeta_system.img"
)
if exist "%IMG_DIR%\vbmeta_vendor.img" (
    "%TOOL_PATH%" --disable-verity --disable-verification flash vbmeta_vendor_a "%IMG_DIR%\vbmeta_vendor.img"
    "%TOOL_PATH%" --disable-verity --disable-verification flash vbmeta_vendor_b "%IMG_DIR%\vbmeta_vendor.img"
)

echo.
echo "[3/6] 重启进入 Fastbootd 模式..."
"%TOOL_PATH%" reboot fastboot
timeout /t 6 /nobreak >nul

echo.
echo "[4/6] 清理 COW 临时分区..."
set "PARTITIONS=system system_dlkm system_ext vendor product odm my_bigball my_carrier my_engineering my_heytap my_manifest my_product my_region my_stock odm_dlkm vendor_dlkm"
for %%p in (%PARTITIONS%) do (
    "%TOOL_PATH%" delete-logical-partition %%p_a-cow >nul 2>&1
    "%TOOL_PATH%" delete-logical-partition %%p_b-cow >nul 2>&1
)
echo "COW 分区清理完成"

echo.
echo "[5/6] 刷入所有镜像 A+B 双槽位..."
for %%f in ("%IMG_DIR%\*.img") do (
    set "name=%%~nf"
    set "skip=0"
    for %%s in (%SKIP_LIST%) do (
        if /i "!name!"=="%%s" set "skip=1"
    )
    if !skip! equ 0 (
        echo "正在刷入：!name!_a 和 !name!_b"
        "%TOOL_PATH%" flash !name!_a "%%~f"
        "%TOOL_PATH%" flash !name!_b "%%~f"
    )
)

echo.
echo "[6/6] 刷入 super 分区..."
if exist "%IMG_DIR%\super.img" (
    "%TOOL_PATH%" flash super "%IMG_DIR%\super.img"
)

echo.
echo "[清理] 清除 FRP 锁..."
"%TOOL_PATH%" erase frp

echo.
echo "[设置] 强制切换并固定 A 槽..."
"%TOOL_PATH%" set_active a

echo.
echo ==========================================================
echo "              刷机全部完成！"
echo "            按任意键重启手机"
echo ==========================================================
pause >nul
"%TOOL_PATH%" reboot
